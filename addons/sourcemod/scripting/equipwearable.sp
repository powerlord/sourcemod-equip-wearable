/* Plugin Template generated by Pawn Studio */

#include <sourcemod>
#include <sdktools>

public Plugin:myinfo = 
{
	name = "EquipWearable",
	author = "Powerlord",
	description = "Quick API for EquipWearable so I don't have to have this in every plugin involving wearables.",
	version = "1.0",
	url = "<- URL ->"
}

new Handle:hGameConf;
new Handle:hEquipWearable;
new Handle:hRemoveWearable;

public APLRes:AskPluginLoad2(Handle:myself, bool:late, String:error[], err_max)
{
	RegPluginLibrary("equipwearable");
	
	CreateNative("EquipPlayerWearable", Native_EquipWearable);
	CreateNative("RemovePlayerWearable", Native_RemoveWearable);
	
	return APLRes_Success;
}

public OnPluginStart()
{
	hGameConf = LoadGameConfigFile("equipwearable");
	
	StartPrepSDKCall(SDKCall_Player);
	PrepSDKCall_SetFromConf(hGameConf, SDKConf_Virtual, "EquipWearable");
	PrepSDKCall_AddParameter(SDKType_CBaseEntity, SDKPass_Pointer);
	hEquipWearable = EndPrepSDKCall();
	
	StartPrepSDKCall(SDKCall_Player);
	PrepSDKCall_SetFromConf(hGameConf, SDKConf_Virtual, "RemoveWearable");
	PrepSDKCall_AddParameter(SDKType_CBaseEntity, SDKPass_Pointer);
	hRemoveWearable = EndPrepSDKCall();
}

public Native_EquipWearable(Handle:plugin, numparams)
{
	new client = GetNativeCell(1);
	if (client < 1 || client > MaxClients || !IsClientInGame(client))
	{
		ThrowNativeError(SP_ERROR_NATIVE, "Client %d is invalid", client);
	}
	
	new wearable = GetNativeCell(2);
	if (wearable <= MaxClients || !IsValidEntity(wearable))
	{
		ThrowNativeError(SP_ERROR_NATIVE, "Entity %d is invalid", wearable);
	}
	
	SDKCall(hEquipWearable, client, wearable);
}

public Native_RemoveWearable(Handle:plugin, numparams)
{
	new client = GetNativeCell(1);
	if (client < 1 || client > MaxClients || !IsClientInGame(client))
	{
		ThrowNativeError(SP_ERROR_NATIVE, "Client %d is invalid", client);
	}
	
	new wearable = GetNativeCell(2);
	if (wearable <= MaxClients || !IsValidEntity(wearable))
	{
		ThrowNativeError(SP_ERROR_NATIVE, "%d is invalid", wearable);
	}
	
	SDKCall(hRemoveWearable, client, wearable);
}
